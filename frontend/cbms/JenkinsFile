pipeline {
  agent any

  options {
    timestamps()
    disableConcurrentBuilds()
  }

  environment {
    // Git repository URL
    REPO_URL = 'https://github.com/ksygt728/BasicTemplate_frontend.git'

    // Git workspace 기준 docker-compose 파일 경로
    COMPOSE_FILE_PATH = 'frontend/cbms/docker-compose.yml'

    // 원격 서버 기준 배포 경로
    DEPLOY_PATH = '/srv/docker'

    // Docker registry info (remote deploy 기준)
    REGISTRY   = 'docker.io/ksygt728'
    IMAGE_NAME = 'cbms-front'
  }

  stages {

    stage('Init Params') {
      steps {
        echo "================ Jenkins Build Parameters ================"
        echo "ENV               : ${params.ENV}"
        echo "BRANCH            : ${params.BRANCH}"
        echo "DEPLOY_SERVER     : ${params.DEPLOY_SERVER}"
        echo "COMPOSE_FILE_PATH : ${COMPOSE_FILE_PATH}"
        echo "※ localhost / 127.0.0.1 은 Jenkins와 동일한 서버(호스트)"
        echo "=========================================================="
      }
    }

    stage('Checkout') {
      steps {
        echo "================ [CHECKOUT] Start Git Checkout ================"
        git branch: params.BRANCH,
            credentialsId: 'git-credential',
            url: env.REPO_URL
        echo "================ ✅ [CHECKOUT] Completed ================"
      }
    }

    stage('Prepare Tag') {
      steps {
        script {
          echo "================ [TAG] Generate image tag ================"

          def tz = TimeZone.getTimeZone('Asia/Seoul')
          def dateStr = new Date().format('yyyy-MM-dd_HH', tz)
          env.TAG = "front-${params.ENV}-${dateStr}-${env.BUILD_NUMBER}"

          echo "▶▶ [TAG] Generated TAG : ${env.TAG}"
          echo "================ ✅ [TAG] Completed ================"
        }
      }
    }

    stage('Docker Build') {
      steps {
        echo "================ [DOCKER] Build image ================"
        sh """
          cd frontend/cbms
          docker build -t ${REGISTRY}/${IMAGE_NAME}-${params.ENV}:${TAG} .
        """
        echo "================ ✅ [DOCKER] Build completed ================"
      }
    }

    stage('Docker Push (Remote Only)') {
      when {
        expression {
          !(params.DEPLOY_SERVER == 'localhost' ||
            params.DEPLOY_SERVER == '127.0.0.1')
        }
      }
      steps {
        echo "================ [DOCKER] Push image to registry ================"
        sh "docker push ${REGISTRY}/${IMAGE_NAME}-${params.ENV}:${TAG}"
        echo "================ ✅ [DOCKER] Push completed ================"
      }
    }

    stage('Deploy') {
      steps {
        script {
          def isLocal =
            params.DEPLOY_SERVER == 'localhost' ||
            params.DEPLOY_SERVER == '127.0.0.1'

          if (isLocal) {
            echo "================ [DEPLOY][LOCAL] Detected ================"
            echo "▶▶ Jenkins 동일 서버에서 docker compose 실행 (SSH 없음)"

            //${WORKSPACE} = Git을 checkout한 “작업 디렉토리”의 절대경로
            sh """
              cd ${WORKSPACE}
              IMAGE_TAG=${TAG} ENV=${params.ENV} \
              docker compose up -d
            """

            echo "================ ✅ [DEPLOY][LOCAL] Completed ================"

          } else {
            echo "================ [DEPLOY][REMOTE] Detected ================"
            echo "▶▶ Target server : ${params.DEPLOY_SERVER}"

            sshagent(credentials: ['git-credential']) {
              sh """
                echo '▶▶ Copy docker-compose from Git workspace'
                scp ${COMPOSE_FILE_PATH} \
                  deploy@${params.DEPLOY_SERVER}:${DEPLOY_PATH}/docker-compose.yml

                echo '▶▶ Execute docker pull & compose'
                ssh deploy@${params.DEPLOY_SERVER} '
                  docker pull ${REGISTRY}/${IMAGE_NAME}-${params.ENV}:${TAG}
                  cd ${DEPLOY_PATH}
                  IMAGE_TAG=${TAG} ENV=${params.ENV} \
                  docker compose up -d
                '
              """
            }

            echo "================ ✅ [DEPLOY][REMOTE] Completed ================"
          }
        }
      }
    }

    stage('Git Tag') {
      steps {
        script {
          def isLocal =
            params.DEPLOY_SERVER == 'localhost' ||
            params.DEPLOY_SERVER == '127.0.0.1'

          if (isLocal) {
            echo "▶▶ [GIT][LOCAL] Skip Git tagging"
            echo "▶▶ [GIT][LOCAL] TAG = ${TAG}"
          } else {
            echo "▶▶ [GIT][REMOTE] Create Git tag"

            sh """
              git tag -a ${TAG} -m "front deploy ${params.ENV}"
              git push origin ${TAG}
            """

            echo "✅ [GIT][REMOTE] Tag pushed"
          }
        }
      }
    }
  }
}