pipeline {
  agent any

  options {
    timestamps()
    disableConcurrentBuilds()
  }

  environment {
    // Git repository URL
    REPO_URL = 'https://github.com/ksygt728/BasicTemplate_frontend.git'

    // Git workspace 기준 docker-compose 파일 경로
    COMPOSE_FILE_PATH = 'frontend/cbms'

    // 원격 서버 기준 배포 경로
    DEPLOY_PATH = '/Users/seungyeonkim/deploy/docker'

    // Docker registry info (remote deploy 기준)
    REGISTRY   = 'docker.io/ksygt728'
    IMAGE_NAME = 'cbms-front'

    // SSH 키 경로 (Jenkins 컨테이너 내부)
    SSH_KEY_PATH = '/var/jenkins_home/.ssh/id_ed25519'

    // 전역으로 사용할 Local 판단 변수
    IS_LOCAL = "${params.DEPLOY_SERVER == 'localhost' || params.DEPLOY_SERVER == '127.0.0.1'}"
  }

  stages {

    stage('Init Params') {
      steps {
        echo "================ Jenkins Build Parameters ================"
        echo "ENV               : ${params.ENV}"
        echo "BRANCH            : ${params.BRANCH}"
        echo "DEPLOY_SERVER     : ${params.DEPLOY_SERVER}"
        echo "COMPOSE_FILE_PATH : ${COMPOSE_FILE_PATH}"
        echo "※ localhost / 127.0.0.1 은 Local 배포, Deploy 단계 스킵. 직접 실행 필요"
        echo "=========================================================="
      }
    }

    stage('Checkout') {
      steps {
        echo "================ [CHECKOUT] Start Git Checkout ================"
        git branch: params.BRANCH,
            credentialsId: 'git-credential',
            url: env.REPO_URL
        echo "================ ✅ [CHECKOUT] Completed ================"
      }
    }

    stage('Prepare Tag') {
      steps {
        script {
          echo "================ [TAG] Generate image tag ================"
          def tz = TimeZone.getTimeZone('Asia/Seoul')
          def dateStr = new Date().format('yyyy-MM-dd_HH', tz)
          env.TAG = "front-${params.ENV}-${dateStr}-${env.BUILD_NUMBER}"
          echo "▶▶ [TAG] Generated TAG : ${env.TAG}"
          echo "================ ✅ [TAG] Completed ================"
        }
      }
    }

    stage('Docker Build') {
      steps {
        echo "================ [DOCKER] Build image ================"
        sh """
          cd frontend/cbms
          docker build -t ${REGISTRY}/${IMAGE_NAME}-${params.ENV}:${TAG} .
        """
        echo "================ ✅ [DOCKER] Build completed ================"
      }
    }

    stage('Docker Push (Remote Only)') {
      when {
        expression { env.IS_LOCAL != "true" }
      }
      steps {
        echo "================ [DOCKER] Push image to registry ================"
        sh "docker push ${REGISTRY}/${IMAGE_NAME}-${params.ENV}:${TAG}"
        echo "================ ✅ [DOCKER] Push completed ================"
      }
    }

    stage('Deploy') {
      when {
        expression { env.IS_LOCAL != "true" }
      }
      steps {
        echo "================ [DEPLOY][REMOTE] Detected ================"
        echo "▶▶ Target server : ${params.DEPLOY_SERVER}"

        sshagent(credentials: ['git-credential']) {
          sh """
            ssh deploy@${params.DEPLOY_SERVER} "mkdir -p ${DEPLOY_PATH}"
            scp ${WORKSPACE}/${COMPOSE_FILE_PATH}/docker-compose.yml deploy@${params.DEPLOY_SERVER}:${DEPLOY_PATH}/docker-compose.yml
            scp ${WORKSPACE}/${COMPOSE_FILE_PATH}/.env.${params.ENV} deploy@${params.DEPLOY_SERVER}:${DEPLOY_PATH}/.env.${params.ENV}

            ssh deploy@${params.DEPLOY_SERVER} '
              docker pull ${REGISTRY}/${IMAGE_NAME}-${params.ENV}:${TAG}
              cd ${DEPLOY_PATH}
              IMAGE_TAG=${TAG} ENV=${params.ENV} FRONT_PORT=3000 docker compose up -d --build
            '
          """
        }

        echo "================ ✅ [DEPLOY][REMOTE] Completed ================"
      }
    }

    stage('Git Tag') {
      when {
        expression { env.IS_LOCAL != "true" }
      }
      steps {
        echo "▶▶ [GIT][REMOTE] Create Git tag"
        sh """
          git tag -a ${TAG} -m "front deploy ${params.ENV}"
          git push origin ${TAG}
        """
        echo "✅ [GIT][REMOTE] Tag pushed"
      }
    }
  }
}