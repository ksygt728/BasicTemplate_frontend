pipeline {
  agent any

  options {
    timestamps()
    disableConcurrentBuilds()
  }

  environment {
    // Git (SSH)
    REPO_URL = 'git@github.com:ksygt728/BasicTemplate_frontend.git'
    
    // Git Tag 계정정보
    GIT_TAG_ID = 'ksygt728'
    GIT_TAG_EMAIL = 'ksygt728@gmail.com'

    // Git workspace 기준 docker-compose 파일 경로
    COMPOSE_FILE_PATH = 'frontend/cbms'

    // 원격 서버 기준 배포 경로
    DEPLOY_PATH = '/home/ec2-user/deploy'

    // Docker registry info
    REGISTRY   = 'docker.io/seungyeon728'
    IMAGE_NAME = 'cbms-front'

    // Local 여부 판단
    IS_LOCAL = "${params.DEPLOY_SERVER == 'localhost' || params.DEPLOY_SERVER == '127.0.0.1'}"
  }

  stages {

    stage('Init Params') {
      steps {
        echo "================ Jenkins Build Parameters ================"
        echo "ENV           : ${params.ENV}"
        echo "BRANCH        : ${params.BRANCH}"
        echo "DEPLOY_SERVER : ${params.DEPLOY_SERVER}"
        echo "=========================================================="
      }
    }

    stage('Checkout (SSH)') {
      steps {
        echo "================ [CHECKOUT] Start Git Checkout ================"
        sshagent(credentials: ['git-ssh-key']) {
          sh """
            git clone -b ${params.BRANCH} ${REPO_URL} .
          """
        }
        echo "================ ✅ [CHECKOUT] Completed ================"
      }
    }

    stage('Prepare Tag') {
      steps {
        script {
          def tz = TimeZone.getTimeZone('Asia/Seoul')
          def dateStr = new Date().format('yyyy-MM-dd_HH-mm', tz)
          env.TAG = "front-${params.ENV}-${dateStr}-${env.BUILD_NUMBER}"
          echo "▶▶ Generated TAG : ${env.TAG}"
        }
      }
    }

    stage('Docker Build') {
      steps {
        echo "================ [DOCKER] Build image ================"
        sh """
          cd ${COMPOSE_FILE_PATH}
          docker build \
            --build-arg ENV_NAME=${params.ENV} \
            -t ${REGISTRY}/${IMAGE_NAME}-${params.ENV}:${TAG} .
        """
        echo "================ ✅ [DOCKER] Build completed ================"
      }
    }

    stage('Docker Push (Remote Only)') {
      when {
        expression { env.IS_LOCAL != "true" }
      }
      steps {
        echo "================ [DOCKER] Push image ================"
        withCredentials([usernamePassword(
          credentialsId: 'dockerhub-credential',
          usernameVariable: 'DOCKER_USER',
          passwordVariable: 'DOCKER_PASS'
        )]) {
          sh """
            echo "\$DOCKER_PASS" | docker login -u "\$DOCKER_USER" --password-stdin
            docker push ${REGISTRY}/${IMAGE_NAME}-${params.ENV}:${TAG}
            docker logout
          """
        }
      }
    }

    stage('Deploy (Remote)') {
      when {
        expression { env.IS_LOCAL != "true" }
      }
      steps {
        echo "================ [DEPLOY][REMOTE] Start ================"
        sshagent(credentials: ['deploy-ssh-key']) {
          sh """
            ssh -o StrictHostKeyChecking=no ec2-user@${params.DEPLOY_SERVER} "mkdir -p ${DEPLOY_PATH}"

            scp -o StrictHostKeyChecking=no \
              ${WORKSPACE}/${COMPOSE_FILE_PATH}/docker-compose.yml \
              ec2-user@${params.DEPLOY_SERVER}:${DEPLOY_PATH}/docker-compose.yml

            scp -o StrictHostKeyChecking=no \
              ${WORKSPACE}/${COMPOSE_FILE_PATH}/.env.${params.ENV} \
              ec2-user@${params.DEPLOY_SERVER}:${DEPLOY_PATH}/.env.${params.ENV}

            ssh -o StrictHostKeyChecking=no ec2-user@${params.DEPLOY_SERVER} "
              docker pull ${REGISTRY}/${IMAGE_NAME}-${params.ENV}:${TAG} && \
              cd ${DEPLOY_PATH} && \
              REGISTRY=${REGISTRY} \
              IMAGE_NAME=${IMAGE_NAME} \
              ENV=${params.ENV} \
              IMAGE_TAG=${TAG} \
              FRONT_PORT=3000 \
              docker-compose up -d
            "
          """
        }
        echo "================ ✅ [DEPLOY][REMOTE] Completed ================"
      }
    }

    stage('Git Tag') {
      when {
        expression { env.IS_LOCAL != "true" }
      }
      steps {
        echo "▶▶ [GIT] Create & Push Tag (SSH)"
        sshagent(credentials: ['git-ssh-key']) {
          sh """
            git config user.name ${GIT_TAG_ID}
            git config user.email ${GIT_TAG_EMAIL}

            git tag -a ${TAG} -m "front deploy ${params.ENV}"
            git push origin ${TAG}
          """
        }
        echo "✅ [GIT] Tag pushed"
      }
    }
  }
}